# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: Tom Gundersen <teg@jklm.no>

pkgname=filesystem
pkgver=2024.03.18
pkgrel=2
pkgdesc='Base Pacman for Android files'
arch=('any')
license=('GPL-3.0-or-later')
url='https://archlinux.org'
depends=('iana-etc')
options=(!strip)
backup=('data/etc/group' 'data/etc/gshadow' 'data/etc/host.conf'
        'data/etc/hosts' 'data/etc/issue' 'data/etc/ld.so.conf' 'data/etc/nsswitch.conf'
        'data/etc/passwd' 'data/etc/profile' 'data/etc/resolv.conf' 'data/etc/securetty'
        'data/etc/shadow' 'data/etc/shells' 'data/etc/subuid' 'data/etc/subgid')
source=('crypttab' 'env-generator' 'fstab' 'group' 'gshadow' 'host.conf' 'hosts'
        'issue' 'ld.so.conf' 'locale.sh' 'nsswitch.conf' 'os-release' 'profile'
        'passwd' 'resolv.conf' 'securetty' 'shadow' 'shells' 'sysctl' 'sysusers'
        'tmpfiles' 'subuid' 'subgid')
sha256sums=('e03bede3d258d680548696623d5979c6edf03272e801a813c81ba5a5c64f4f82'
            '4bc62ac505636e4ac1b79096de41cfd0d0610a4fb17249a7172a66114d86db29'
            'e54626e74ed8fee4173b62a545ab1c3a3a069e4217a0ee8fc398d9933e9c1696'
            'f9060207071192273e9b6147ffb67f7461c48214a579a447f6d1a6ae2b0e7481'
            '90d879374f77bac47f132164c1e7fc4892e994ff1d1ac376efa0c1c26ea37273'
            '4d7b647169063dfedbff5e1e22cee77bd1a4183dbcfd5e802e68939da4bbf733'
            'd9cd8a77d9e0aa5e90d7f4ed74c8745c17b525e720e28e4c44364150003c35f9'
            '8789b91c6dccdc338c29442e38d7de420464a26982cf0669136126553d81d29a'
            '022ee4c04810877d03949b31db22f866107bbec9313ee115b23bd965c01120cc'
            'ede40050533aa3633aaa79d2a6d0e73bed9fe2142b7bd51eb4d609dca7ca10f9'
            'c8ee7a9faf798caab178ec51afae4146f1efd8a716b7acedf28345b6c75f9697'
            'f1888d2cc91758678a0c2b71711ea05d01dad2d1eb46d0caf3f5c9f154db207f'
            'e18bcbd443978a293d168c51371123548638523308021fbcabf5a543be108ba5'
            '7bcdbe08c07305eeb2f16ef12fdc1cdd78b6197cb4f27216c1f3fcee5a6ad227'
            '07fc22e0c1499bef7d5a0ebdb770a15d98bf5a76fec633b26defea5a2a9c3160'
            'd88be2b45b43605ff31dd83d6a138069b6c2e92bc8989b7b9ab9eba8da5f8c7b'
            '6e13705ac4d6f69cdba118c6b70c722346fd3c45224133e6bbfe28aca719563c'
            '1cbad183d43dd50af928a2cb3f19e6ced87ee27335e9da11bae1ebb36fe3264a'
            '89e43a0b7028f52d5c8e7fb961d962c4b4f4e9595880a6157274ddb2c7c0b6b4'
            '30b97e8f5965744138f7a394e04454db6d509fb89e0a9b615bcd9037df3d6f2a'
            '5d8e61479f0093852365090e84d8d95b1e7fccfab068274ee25863bde6ff3e07'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855')

package() {
  cd "$pkgdir"
  install -d -m771 data
  cd data

  # setup root filesystem
  for d in etc home usr var opt; do
    install -d -m755 $d
  done

  # setup /data/etc and /data/usr/share/factory/etc
  install -d etc/{ld.so.conf.d,skel,profile.d} usr/share/factory/etc
  for f in group host.conf hosts issue ld.so.conf nsswitch.conf \
  passwd resolv.conf securetty shells profile subuid subgid; do
    install -m644 "$srcdir"/$f etc/
    install -m644 "$srcdir"/$f usr/share/factory/etc/
  done
  ln -s /proc/self/mounts etc/mtab
  for f in gshadow shadow; do
    install -m600 "$srcdir"/$f etc/
    install -m600 "$srcdir"/$f usr/share/factory/etc/
  done
  touch etc/arch-release
  install -m644 "$srcdir"/locale.sh etc/profile.d/locale.sh
  install -Dm644 "$srcdir"/os-release usr/lib/os-release

  # setup /var
  for d in cache local opt log/old lib/misc empty run lock; do
    install -d -m755 var/$d
  done
  install -d -m1777 var/{tmp,spool/mail}

  ln -s spool/mail var/mail

  # setup /usr hierarchy
  for d in bin include lib share/{misc,pixmaps} src; do
    install -d -m755 usr/$d
  done
  for d in {1..8}; do
    install -d -m755 usr/share/man/man$d
  done
  install -d usr/lib/ld.so.conf.d

  # # add lib symlinks
  # ln -s usr/lib lib
  # [[ $CARCH = 'aarch64' ]] && {
  #   ln -s usr/lib lib64
  #   ln -s lib usr/lib64
  # }

  # add bin symlinks
  # ln -s usr/bin bin
  # ln -s usr/bin sbin
  # ln -s bin usr/sbin

  # setup /usr/local hierarchy
  for d in bin etc games include lib man sbin share src; do
    install -d -m755 usr/local/$d
  done
  ln -s ../man usr/local/share/man

  # # add logo
  # install -D -m644 "$srcdir"/archlinux-logo{.png,.svg,-text.svg,-text-dark.svg} usr/share/pixmaps
}

# vim:set ts=2 sw=2 et:
